apply plugin: 'application'

dependencies {
	implementation project(':org.xtext.example.mydsl')
	implementation "org.eclipse.xtext:org.eclipse.xtext.ide:${xtextVersion}"
	implementation "org.eclipse.xtext:org.eclipse.xtext.xbase.ide:${xtextVersion}"
	implementation "org.eclipse.lsp4j:org.eclipse.lsp4j:0.8.0"
}

// Add a start script for the socket case
mainClassName = 'org.xtext.example.mydsl.ide.RunServer'
startScripts {
	applicationName = 'mydsl-socket'
}

// Add another start script for the standalone / self-contained case
task standaloneStartScript(type: CreateStartScripts) {
	mainClassName = 'org.eclipse.xtext.ide.server.ServerLauncher'
	applicationName = 'mydsl-standalone'
	outputDir = new File(project.buildDir, 'scripts')
	classpath = jar.outputs.files + project.configurations.runtimeClasspath
}

applicationDistribution.into("bin") {
	duplicatesStrategy "include"
	from(standaloneStartScript)
	fileMode = 0755
}